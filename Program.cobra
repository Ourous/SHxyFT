use System.Drawing
use System.Windows.Forms
use System.Threading
use System.ComponentModel

class Program
	"""
	Program
	"""
	#TODO add better description

	event PreviewRefresh as PreviewRefreshHandler is shared
	sig PreviewRefreshHandler(img as Bitmap)

	shared
		get renderThread from __renderThread as BackgroundWorker = BackgroundWorker(workerSupportsCancellation=true,workerReportsProgress=true)
		var __sourceImage as Bitmap?
		pro sourceImage as Bitmap
			get
				return __sourceImage to !
			set
				__sourceImage = value
				__history = [value]
				__historyIndex = 0
				raise .PreviewRefresh, .currentImage

		get currentImage as Bitmap
			return Bitmap(__history[__historyIndex])

		#handles undo/redo operations behind the scenes
		#TODO save history in a more memory-friendly way
		var __history as List<of Bitmap>?
		var __historyIndex as int

		def undo
			if __historyIndex > 0
				__historyIndex -= 1
				raise .PreviewRefresh, .currentImage

		def redo
			if __historyIndex < __history.count-1
				__historyIndex += 1
				raise .PreviewRefresh, .currentImage

		def main has STAThread
			listen __renderThread.doWork, ref Renderer.render
			listen __renderThread.runWorkerCompleted, do(sender, e as RunWorkerCompletedEventArgs)
				try
					img = e.result to Bitmap
				catch ex as Exception
					print ex.message
				success
					__history = __history[:__historyIndex+=1]+[img]
					raise .PreviewRefresh, img
			try
				Application.enableVisualStyles
				Application.run(Window())
			catch ex as Exception
				print ex.message
			
